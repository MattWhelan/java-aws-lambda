buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://jcenter.bintray.com'
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
}

apply plugin: 'com.github.sherter.google-java-format'
apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenCentral()
}

project.configurations.configure {
    "sonatype"() {
        extendsFrom archives
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    implementation 'com.amazonaws:aws-lambda-java-core:1.1.0'
    // 2.2.2 is earliest version that has all needed event sources
    implementation 'com.amazonaws:aws-lambda-java-events:2.2.2'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.163'
    implementation 'com.amazonaws:aws-java-sdk-kinesis:1.11.163'
    implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.163'
    implementation 'com.googlecode.json-simple:json-simple:1.1'
    implementation('io.opentracing:opentracing-api:0.33.0')
    implementation('io.opentracing:opentracing-util:0.33.0')
    implementation('io.opentracing:opentracing-noop:0.33.0')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'io.opentracing:opentracing-mock:0.33.0'
}

jar {
    from(projectDir) { include "README.md" }

    manifest {
        attributes 'Implementation-Title': 'AWS Lambda OpenTracing Java SDK',
                'Implementation-Version': project.version,
                'Created-By': 'New Relic, Inc',
                'Built-Date': new Date(),
                'Specification-Version': project.version,
                'Build-Id': System.getProperty('BUILD_ID') || "None"
    }
}

task uploadSonatype(type: Upload) {
    configuration = configurations.sonatype
    uploadDescriptor = true
}

task customSourcesJar(type: Jar) {
    classifier = 'sources'
    from(projectDir) { include "README.md" }
    from(sourceSets.main.allSource) 

    manifest {
        attributes 'Implementation-Title': 'AWS Lambda OpenTracing Java SDK Sources',
                'Implementation-Version': project.version,
                'Created-By': 'New Relic, Inc',
                'Built-Date': new Date(),
                'Specification-Version': project.version,
                'Build-Id': System.getProperty('BUILD_ID') || "None"
    }
}

artifacts {
    archives customSourcesJar
}

extraArchive {
    sources = false
}

nexus {
    sign = true
    configuration = "sonatype"
}

uploadSonatype.doFirst {
    configuration.artifacts.each {
        println(project.name + " uploading: " + it)
    }
}

def customizePom(pom, nameIn, descriptionIn) {
    pom.project {
        url 'https://newrelic.com/'
        name nameIn
        description descriptionIn

        licenses {
            license { url 'https://github.com/newrelic/java-aws-lambda/blob/master/LICENSE' }
        }

        scm {
            url "git@github.com:newrelic/java-aws-lambda.git"
            connection "scm:git:git@github.com:newrelic/java-aws-lambda.git"
        }

        developers {
            developer {
                id 'newrelic'
                name 'New Relic'
                email 'support@newrelic.com'
            }
        }
    }
}

// For external deploys (e.g. - release)
modifyPom { pom ->
    customizePom(pom, 'AWS Lambda OpenTracing Java SDK',
            'OpenTracing Java SDK for instrumenting AWS Lambda functions.')
}
